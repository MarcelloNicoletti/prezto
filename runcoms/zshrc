#! /usr/bin/env zsh
#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

# Disable username in prompt
DEFAULT_USER="$(whoami)"

# Functions
if [[ "$OSTYPE" == darwin* ]]; then
  function filemerge {
    /Applications/Xcode.app/Contents/Applications/FileMerge.app/Contents/MacOS/FileMerge -left $1 -right $2
  }

  function mactrash () {
    local dir="$(pwd)"
    for partial in $@; do
      local file="$dir/$partial"
      if [[ $partial == /* ]]; then
        file="$partial"
      fi
      if [ -e "$file" ]; then
        osascript -e "
set posixFile to POSIX file \"$file\"
tell application \"Finder\"
  move posixFile to trash
end tell
"
      else
        echo "$partial not found"
      fi
    done
  }
fi

# History Adjustments
SAVEHIST=100000
HIST_IGNORE_SPACE="YES"
HISTFILE="${ZDOTDIR:-$HOME}/.zhistory"
alias exit=" exit"
alias clear=" clear"

# Rbenv initialization
if (( $+commands[rbenv] )); then
  eval "$(rbenv init -)"
fi

# Jenv initialization
if (( $+commands[jenv] )); then
  eval "$(jenv init -)"
fi

# Nodenv initialization
if (( $+commands[nodenv] )); then
  eval "$(nodenv init -)"
fi

# tmux aliases
if (( $+commands[tmux] )); then
  function ttmux () {
    if (( ${+TMUX} )); then
      echo "Don't nest tmux sessions with this exec method."
      return
    fi
    exec "tmux" "new-session" "-A" "-s" "tmux"
  }

  function codemux () {
    if (( ${+TMUX} )); then
      echo "Don't nest tmux sessions with this exec method."
      return
    fi
    local dir="$(basename "$(pwd)" | sed "s/[^[:alpha:]^[:digit:]]//g")"
    exec "tmux" "new-session" "-A" "-s" "code-$dir"
  }
fi

# time aliases
alias 8601="date -u +\"%FT%TZ\""

# Diffing
if (( $+commands[colordiff] )); then
  alias diff=colordiff
fi

# Note: meant for single files
alias gitdiff="git diff --no-index --"

# Local computer rcs
if [[ -e "${ZDOTDIR:-$HOME}/.zlocal" ]]; then
  source "${ZDOTDIR:-$HOME}/.zlocal"
fi
